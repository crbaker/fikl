start: instruction

instruction: "select" subset collection_type subject [where] [limit] -> select_collection
    | "select" subset document_type subject -> select_document

    | "update" collection_type subject "set" set where  -> update_collection
    | "update" document_type subject "set" set -> update_document

    | "delete" collection_type subject where -> delete_collection
    | "delete" document_type subject -> delete_document

    | "show" "collections" -> show_collections

where: "where" comparrison ("and" comparrison)*
comparrison: property operator matching

matching: (literal | array)

limit: "limit" SIGNED_NUMBER

subset: (all | fields)
all: ALL
fields: property ("," property)*

set: setter ("," setter)*
setter: property "=" literal

property: ESCAPED_STRING | CNAME
subject: ESCAPED_STRING | CNAME
literal: ESCAPED_STRING | NUMBER | SIGNED_NUMBER | NULL

array: "[" literal ("," literal)* "]"

ALL: "*"

WITHIN: "within"
FROM: "from"
AT: "at"

NULL: "null"

collection_type: WITHIN|FROM
document_type: AT
subject_type: collection_type | document_type

GREATER: ">"
GREATER_EQUAL: ">="
LESSER: "<"
LESSER_EQUAL: "<="
NOT: "!="
EQUAL: "=="
IN: "in"
NOT_IN: "not-in"
ARRAY_CONTAINS: "array_contains"
ARRAY_CONTAINS_ANY: "array_contains_any"

operator: LESSER | LESSER_EQUAL | EQUAL | NOT | GREATER_EQUAL | GREATER | IN | NOT_IN | ARRAY_CONTAINS | ARRAY_CONTAINS_ANY

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER
%import common.SIGNED_NUMBER
%import common.WS
%ignore WS